Функция СоздатьПрофиль(Знач SMTP = Истина, Знач POP3 = Истина, Знач IMAP = Истина)

	Профиль = Новый ИнтернетПочтовыйПрофиль;
	
	Профиль.Таймаут = Таймаут;

	Если SMTP Тогда

		Профиль.АдресСервераSMTP = Сервер;
		Профиль.ПользовательSMTP = Пользователь;
		Профиль.ПарольSMTP = Пароль;
		Профиль.ПортSMTP = ПортSMTP;
		Профиль.ИспользоватьSSLSMTP = ИспользоватьSSLSMTP;

	КонецЕсли;

	Если POP3 Тогда

		Профиль.АдресСервераPOP3 = СерверPop3;
		Профиль.ИспользоватьSSLPOP3 = ИспользоватьSSLPOP3;
		Профиль.Пользователь = Пользователь;
		Профиль.Пароль = Пароль;

	КонецЕсли;

	Если IMAP Тогда

		Профиль.АдресСервераImap = СерверImap;
		Профиль.ПользовательImap = Пользователь;
		Профиль.ПарольImap = Пароль;
		Профиль.ИспользоватьSSLIMAP = ИспользоватьSSLIMAP;

	КонецЕсли;

	Возврат Профиль;
	
КонецФункции

Функция СоздатьСообщение(Знач Получатель, Знач Тема, Знач ТекстСообщения)

	Сообщение = Новый ИнтернетПочтовоеСообщение;
	Сообщение.Получатели.Добавить(Получатель);
	Сообщение.ОбратныйАдрес.Добавить(Отправитель);
	Сообщение.Отправитель = Отправитель;
	Сообщение.Тема = Тема;
	Сообщение.Тексты.Добавить(ТекстСообщения);

	Возврат Сообщение;

КонецФункции

Процедура Проверка_ОтправкаПрием_SMTP_POP3()

	Профиль = СоздатьПрофиль(Истина, Истина, Ложь);
	Почта = Новый ИнтернетПочта;
	Почта.Подключиться(Профиль, ПротоколИнтернетПочты.POP3);

	ТекстПисьма = "TestMessage";
	ТемаПисьма = "1-2-3";

	СтарыеИдентификаторы = Почта.ПолучитьИдентификаторы();

	Сообщение = СоздатьСообщение(Отправитель, ТемаПисьма, ТекстПисьма);
	Почта.Послать(Сообщение, , ПротоколИнтернетПочты.SMTP);

	КоличествоПопыток = 5;

	Для НомерПопытки = 1 По КоличествоПопыток Цикл

		Почта.Отключиться();
		Приостановить(1000); // Подождём на всякий случай
		Сообщить("Попытка " + НомерПопытки + "...");

		Почта.Подключиться(Профиль, ПротоколИнтернетПочты.POP3);

		НовыеИдентификаторы = Почта.ПолучитьИдентификаторы(СтарыеИдентификаторы);

		// Ожидаем, что НовыеИдентификаторы().Количество() = 1

		Если НовыеИдентификаторы.Количество() = 0 Тогда

			Сообщить("Письмо ещё не дошло...");
			Продолжить;

		ИначеЕсли Не (НовыеИдентификаторы.Количество() = 1) Тогда

			ВызватьИсключение "Всё плохо: новое письмо не обозначилось!";

		КонецЕсли;

		Письма = Почта.Выбрать(Истина, НовыеИдентификаторы);
		Если Не (Письма.Количество() = 1) Тогда
			ВызватьИсключение "Всё плохо: новое письмо не найдено по идентификатору!";
		КонецЕсли;

		Письмо = Письма.Получить(0);
		Если Не Письмо.Тема = ТемаПисьма Тогда
			ВызватьИсключение "Получено не то письмо (Тема)!";
		КонецЕсли;

		Если Не СокрЛП(Письмо.Тексты.Получить(0).Текст) = ТекстПисьма Тогда

			Для Каждого мТекст Из Письмо.Тексты Цикл

				Сообщить("-------");
				Сообщить(мТекст.Текст);

			КонецЦикла;

			Сообщить("-- Отправляли: ");
			Сообщить(ТекстПисьма);

			ВызватьИсключение "Получено не то письмо (Текст)!";

		КонецЕсли;

		Сообщить("Письмо успешно получено!");
		Прервать;

	КонецЦикла;

	Почта.Отключиться();

КонецПроцедуры

Процедура Проверка_ОтправкаПрием_IMAP_IMAP()

	Профиль = СоздатьПрофиль(Ложь, Ложь, Истина);
	Почта = Новый ИнтернетПочта;
	Почта.Подключиться(Профиль, ПротоколИнтернетПочты.IMAP);

	ТекстПисьма = "TestMessage";
	ТемаПисьма = "1-2-3";

	СтарыеИдентификаторы = Почта.ПолучитьИдентификаторы();

	Сообщение = СоздатьСообщение(Отправитель, ТемаПисьма, ТекстПисьма);
	Почта.Послать(Сообщение, , ПротоколИнтернетПочты.IMAP);

	Приостановить(2000); // Подождём, чтобы увидеть письмо в почтовике

	КоличествоПопыток = 5;

	Для НомерПопытки = 1 По КоличествоПопыток Цикл

		Почта.Отключиться();
		Приостановить(1000); // Подождём на всякий случай
		Сообщить("Попытка " + НомерПопытки + "...");

		Почта.Подключиться(Профиль, ПротоколИнтернетПочты.IMAP);

		НовыеИдентификаторы = Почта.ПолучитьИдентификаторы(СтарыеИдентификаторы);

		// Ожидаем, что НовыеИдентификаторы().Количество() = 1

		Если НовыеИдентификаторы.Количество() = 0 Тогда

			Сообщить("Письмо ещё не дошло...");
			Продолжить;

		ИначеЕсли Не (НовыеИдентификаторы.Количество() = 1) Тогда

			ВызватьИсключение "Всё плохо: новое письмо не обозначилось!";

		КонецЕсли;

		Письма = Почта.Выбрать(Истина, НовыеИдентификаторы);
		Если Не (Письма.Количество() = 1) Тогда
			ВызватьИсключение "Всё плохо: новое письмо не найдено по идентификатору!";
		КонецЕсли;

		Письмо = Письма.Получить(0);
		Если Не Письмо.Тема = ТемаПисьма Тогда
			ВызватьИсключение "Получено не то письмо (Тема)!";
		КонецЕсли;

		Если Не СокрЛП(Письмо.Тексты.Получить(0).Текст) = ТекстПисьма Тогда

			Для Каждого мТекст Из Письмо.Тексты Цикл

				Сообщить("-------");
				Сообщить(мТекст.Текст);

			КонецЦикла;

			Сообщить("-- Отправляли: ");
			Сообщить(ТекстПисьма);

			ВызватьИсключение "Получено не то письмо (Текст)!";

		КонецЕсли;

		Сообщить("Письмо успешно получено!");
		Почта.ОчиститьУдаленныеСообщения();

		Прервать;

	КонецЦикла;

	Почта.Отключиться();

КонецПроцедуры

Проверка_ОтправкаПрием_SMTP_POP3();
Проверка_ОтправкаПрием_IMAP_IMAP();
